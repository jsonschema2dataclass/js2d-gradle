plugins {
    id 'com.android.application'
    id "org.jsonschema2dataclass"
    id "kotlin-android"
}

android {
    namespace "org.jsonschema2dataclass.android"

    compileSdkVersion 33
    sourceSets {
        debug.java.srcDirs +='build/generated/sources/js2d/main/debug'
        release.java.srcDirs +='build/generated/sources/js2d/main/release'
    }
    defaultConfig {
        applicationId "org.jsonschema2dataclass.android"
        minSdkVersion 17
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }
    compileOptions {
        sourceCompatibility = JavaVersion.current()
        targetCompatibility = JavaVersion.current()
    }
    kotlin {
        jvmToolchain {
            languageVersion.set(JavaLanguageVersion.of(JavaVersion.current().majorVersion.toInteger()))
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'

    implementation 'com.google.code.gson:gson:2.10.1'
    // Required if generating JSR-303 annotations
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation "androidx.core:core-ktx:1.10.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.20"
}

jsonSchema2Pojo {
    executions {
        main {
            io {
                source.setFrom(files("${project.rootDir}/schema"))
                sourceType.set("jsonschema")
                outputEncoding.set("UTF-8")
                delimitersPropertyWord.set("")
            }

            klass {
                targetPackage.set("com.oosocial.clarityn.rest.clarityn.model")
                annotationStyle.set("gson")
                customAnnotatorClass.set("org.jsonschema2pojo.NoopAnnotator")
            }

            methods {
                builders.set(false)
                hashcodeAndEquals.set(true)
                toStringMethod.set(true)
                annotateJsr303.set(true)
            }

            fields {
                usePrimitives.set(false)
                integerUseLong.set(false)
                floatUseDouble.set(true)
                initializeCollections.set(true)
            }

            dateTime.jodaDate.set(false)
        }
    }
}
